/*
 * This file is part of Applied Energistics 2.
 * Copyright (c) 2013 - 2015, AlgorithmX2, All rights reserved.
 *
 * Applied Energistics 2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Applied Energistics 2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Applied Energistics 2.  If not, see <http://www.gnu.org/licenses/lgpl>.
 */

plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.12.4"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
            includeGroup "dev.architectury"
        }
    }
    maven {
        url "https://maven.bai.lol"
        content {
            includeGroup "mcp.mobius.waila"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://maven.parchmentmc.net/"
            }
        }
        filter {
            includeGroup "org.parchmentmc.data"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.17.1:2021.09.05@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntime("mcp.mobius.waila:wthit:fabric-${project.wthit_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
        exclude group: "net.fabricmc.fabric-api", module: "fabric-gametest-api-v1"
    }
    include modImplementation("teamreborn:energy:${project.tr_energy_version}"){
        exclude group: "net.fabricmc.fabric-api"
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // unit test dependencies
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.1")
    testImplementation("org.junit.platform:junit-platform-launcher:1.8.1")
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation("com.google.guava:guava-testlib:21.0")
    testImplementation("org.mockito:mockito-junit-jupiter:3.9.0")
    testImplementation("org.mockito:mockito-inline:3.9.0")
}

group = artifact_group
archivesBaseName = artifact_basename

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

// ensure everything uses UTF-8 and not some random codepage chosen by gradle
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = false
    options.compilerArgs << "-Xmaxerrs" << "9999"
}

///////////////////
// Version Number
version = version_major + "." + version_minor + "." + version_patch

ext.pr = System.getenv('PR_NUMBER') ?: ""
if (ext.pr) {
  version = version + "+pr." + ext.pr
}

ext.branch = System.getenv('BRANCH') ?: ""
if (ext.branch) {
  version = version + "+branch." + ext.branch
}

ext.tag = System.getenv('TAG') ?: ""
if (ext.tag && System.getenv("GITHUB_ENV")) {
  if (!ext.tag.startsWith("fabric/v")) {
      throw new GradleException("Tags for the fabric version should start with fabric/: ${ext.tag}")
  }

  version = ext.tag.substring("fabric/v".length())
  def envFile = new File(System.getenv("GITHUB_ENV"))
  // Validate that the rest is a semver version
  if (version ==~ /^(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?$/) {
    envFile.append("VERSION=${version}")
  } else {
    throw new GradleException("Invalid semver: $version")
  }
}

ext.isAlpha = project.version.contains("alpha")
ext.isBeta = project.version.contains("beta")

sourceSets {
    portaforgy
    main {
        compileClasspath += sourceSets.portaforgy.output
        runtimeClasspath += sourceSets.portaforgy.output
        resources {
            srcDir 'src/generated/resources'
        }
    }
    datagen {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.portaforgy.output
        runtimeClasspath += sourceSets.portaforgy.output
    }
    test {
        compileClasspath += sourceSets.portaforgy.output
        runtimeClasspath += sourceSets.portaforgy.output
    }
}

configurations {
    portaforgyImplementation.extendsFrom(compileClasspath)
    datagenImplementation.extendsFrom(compileClasspath)
    datagenRuntimeOnly.extendsFrom(runtimeClasspath)
}

test {
    useJUnitPlatform()
}

////////////////////
// Forge/Minecraft
loom {
    accessWidenerPath = file("src/main/resources/appliedenergistics2.accesswidener")

    runs {
        datagen {
            client()
            source "datagen"
            property "appeng.generateData", "true"
            property "appeng.generateData.outputPath", file("src/generated/resources").absolutePath
            property "appeng.generateData.existingData", file("src/main/resources").absolutePath
        }
    }
}

//////////////
// Artifacts
processResources {
    exclude '.cache'

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    finalizedBy 'remapJar'

    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir

    manifest {
        attributes([
                "Specification-Title": "Applied Energistics 2",
                "Specification-Vendor": "TeamAppliedEnergistics",
                "Specification-Version": "${project.version}",
                "Implementation-Title": "${project.name}",
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"TeamAppliedEnergistics",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "appliedenergistics2.mixins.json"
        ])
    }
}

def publicApiIncludePatterns = {
    exclude "**/*Internal.*"
    exclude "**/*Internal\$*.*"
    include "appeng/api/**"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath + sourceSets.main.output

    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}
javadoc publicApiIncludePatterns

task javadocJar(type: Jar, dependsOn: javadoc, group: "build") {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}
// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task apiJar(type: Jar, group: "build") {
    archiveClassifier = "api"
    // api jar ist just a development aid and serves as both a binary and source jar simultaneously
    from sourceSets.main.output
    from sourceSets.main.allJava
}
apiJar publicApiIncludePatterns

artifacts {
    archives javadocJar
    archives sourcesJar
    archives apiJar
}

//////////////////
// Maven publish
publishing {
    if (!version.endsWith("-SNAPSHOT")) {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = 'appliedenergistics2-fabric'
                version = project.version

                // add all the jars that should be included when publishing to maven
                artifact(remapJar) {
                    builtBy remapJar
                }
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }
                artifact javadocJar
                artifact apiJar
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("GITHUB_ACTOR")
                password System.getenv("GITHUB_TOKEN")
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/AppliedEnergistics/Applied-Energistics-2"
        }
        maven {
            credentials {
                username System.getenv("MODMAVEN_USER")
                password System.getenv("MODMAVEN_PASSWORD")
            }
            name = "modmaven"
            url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}

/////////////
// Spotless
spotless {

    java {
        target 'src/*/java/appeng/**/*.java'

        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile 'codeformat/codeformat.xml'
        importOrderFile 'codeformat/ae2.importorder'
    }

    format 'json', {
        target 'src/*/resources/**/*.json'
        targetExclude 'src/generated/resources/**'
        prettier().config(['parser': 'json'])
    }

    format 'langJsonOrder', {
        target 'src/**/lang/*.json'
        addStep(LangJsonOrderStep.create())
    }
}

import com.diffplug.spotless.FormatterFunc
import com.diffplug.spotless.FormatterStep
import com.google.gson.Gson
import com.google.gson.GsonBuilder

/**
 * Enforce key ordering in the lang files by parsing the JSON to a TreeMap and writing it back.
 */
final class LangJsonOrderStep {
    private LangJsonOrderStep() {}
    private static final Gson GSON = new GsonBuilder().disableHtmlEscaping().setLenient().setPrettyPrinting().create();

    static FormatterStep create() {
        return FormatterStep.createNeverUpToDate(
                'langJsonOrderStep',
                new FormatterFunc() {
                    String apply(String input) {
                        TreeMap<String, Object> jsonMap = GSON.fromJson(input, TreeMap.class);
                        String sortedJson = GSON.toJson(jsonMap);
                        // Unescape ' (the apostrophe character). GSON escapes it by default.
                        String prettyPrinted = sortedJson.replace('\\u0027', '\'');
                        // Add a return after the last line to respect prettier's formatting of the json files.
                        return prettyPrinted + "\n";
                    }
                },
        );
    }
}

////////////////
// Curse Forge
if (System.getenv("CURSEFORGE") && !version.endsWith("-SNAPSHOT")) {
    def cfReleaseType = "release"
    if (ext.isAlpha) {
      cfReleaseType = "alpha"
    } else if (ext.isBeta) {
      cfReleaseType = "beta"
    }

    curseforge {
        apiKey = System.getenv("CURSEFORGE")
        project {
            id = project.curseforge_project
            changelogType = "markdown"
            changelog = System.getenv("CHANGELOG") ?: "Please visit our [releases](https://github.com/AppliedEnergistics/Applied-Energistics-2/releases) for a changelog"
            releaseType = cfReleaseType
            addGameVersion project.minecraft_version
            addGameVersion "Fabric"
            mainArtifact(remapJar.archiveFile) {
                displayName = "${project.version} [FABRIC]"
            }
        }
        options {
            forgeGradleIntegration = false
        }
        afterEvaluate {
            tasks.getByName("curseforge${project.curseforge_project}").dependsOn remapJar
        }
    }
}

////////////////
// IntelliJ Project Import
// The Mixin annotation process does not have an obfuscation source when running through the IntelliJ compiler,
// thus we have to prevent it from being activated as part of importing this Gradle project into IntelliJ.
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

// See https://github.com/AppliedEnergistics/Applied-Energistics-2/issues/5259
// Gradle module metadata contains mapped dependencies, making our artifacts unconsumable
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
