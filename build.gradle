apply plugin: 'forge'

apply from: 'gradle/scripts/propertyloader.gradle'
apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/artifacts.gradle'
apply from: 'gradle/scripts/autoinstallruntime.gradle'

buildscript {
    repositories {
        mavenCentral()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 7200, 'hours'
}

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

version = config.version + "-" + config.aechannel + "-" + config.build
group = config.group
archivesBaseName = config.archivesBaseName

// If TeamCity is running this build, lets set the version info
if (hasProperty("teamcity")) {
    version = teamcity["build.number"]
}

// Add Coremod Manifest
jar {
    manifest {
        attributes 'FMLCorePlugin': 'appeng.transformer.AppEngCore'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }

    // specify which files are really included, can control which APIs should be in
    include "appeng/**"
    include "assets/**"
    include "mcmod.info"
    include "pack.mcmeta"
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version

    replace "@version@", project.version
    replace "@aechannel@", config.aechannel

    // used when launching minecraft in dev env
    runDir = "run"
}

sourceSets {
    main {
        java {
            srcDirs += 'src/api/java'
            srcDirs += 'src/main/java/'
        }

        resources {
            srcDir "src/main/resources/"
            include "assets/appliedenergistics2/recipes/*.recipe",
                    "assets/appliedenergistics2/lang/*.lang",
                    "assets/appliedenergistics2/textures/blocks/*",
                    "assets/appliedenergistics2/textures/guis/*",
                    "assets/appliedenergistics2/textures/models/*",
                    "assets/appliedenergistics2/textures/items/*"
        }
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': config.minecraft_version
    }

    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}