import org.gradle.api.initialization.resolve.RepositoriesMode
import org.gradle.api.initialization.resolve.RulesMode

pluginManagement {
    plugins {
        id 'com.diffplug.spotless' version '6.25.0'
        id 'io.github.goooler.shadow' version '8.1.7'
        id 'de.undercouch.download' version '5.6.0'
        // https://projects.neoforged.net/neoforged/ModDevGradle
        id 'net.neoforged.moddev' version '1.0.13'
        id 'net.neoforged.moddev.repositories' version '1.0.13'
    }
}

plugins {
    id 'net.neoforged.moddev.repositories'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    rulesMode = RulesMode.FAIL_ON_PROJECT_RULES
    repositories {
        maven {
            url "https://maven.shedaniel.me/"
            content {
                includeGroup "me.shedaniel"
                includeGroup "me.shedaniel.cloth"
                includeGroup "dev.architectury"
            }
        }
        maven {
            url "https://maven2.bai.lol"
            content {
                includeGroup "mcp.mobius.waila"
                includeGroup "lol.bai"
            }
        }
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
            content {
                includeGroup "dev.emi"
            }
        }
        maven {
            name 'cursemaven'
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven { // for TOP
            url "https://maven.k-4u.nl/"
            content {
                includeGroup "mcjty.theoneprobe"
            }
        }
        maven {
            url = "https://maven.theillusivec4.top/"
            content {
                includeGroup "top.theillusivec4.curios"
            }
        }
        maven {
            url = "https://maven.blamejared.com/"
            content {
                includeGroup "mezz.jei"
            }
        }
        mavenCentral()
    }
}

rootProject.name = 'ae2';

include 'libs:markdown'
