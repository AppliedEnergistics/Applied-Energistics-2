import org.gradle.api.initialization.resolve.RepositoriesMode
import org.gradle.api.initialization.resolve.RulesMode

pluginManagement {
    plugins {
        id 'com.diffplug.spotless' version '6.25.0'
        // https://projects.neoforged.net/neoforged/ModDevGradle
        id 'net.neoforged.moddev' version '2.0.107'
        id 'net.neoforged.moddev.repositories' version '2.0.107'
    }
}

plugins {
    id 'net.neoforged.moddev.repositories'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    rulesMode = RulesMode.FAIL_ON_PROJECT_RULES
    repositories {
        maven {
            url = 'https://maven.shedaniel.me/'
            content {
                includeGroup 'me.shedaniel'
                includeGroup 'me.shedaniel.cloth'
                includeGroup 'dev.architectury'
            }
        }
        maven {
            url = 'https://maven2.bai.lol'
            content {
                includeGroup 'mcp.mobius.waila'
                includeGroup 'lol.bai'
            }
        }
        maven {
            name = 'TerraformersMC'
            url = 'https://maven.terraformersmc.com/'
            content {
                includeGroup 'dev.emi'
            }
        }
        maven {
            name = 'cursemaven'
            url = 'https://www.cursemaven.com'
            content {
                includeGroup 'curse.maven'
            }
        }
        maven { // for TOP
            url = 'https://maven.k-4u.nl/'
            content {
                includeGroup 'mcjty.theoneprobe'
            }
        }
        maven {
            url = 'https://maven.theillusivec4.top/'
            content {
                includeGroup 'top.theillusivec4.curios'
            }
        }
        maven {
            url = 'https://maven.blamejared.com/'
            content {
                includeGroup 'mezz.jei'
            }
        }
        mavenLocal() {
            content {
                includeModule('org.appliedenergistics', 'guideme')
            }
        }
        maven {
            name = 'GuideME Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
            content {
                includeModule('org.appliedenergistics', 'guideme')
            }
        }
        maven {
            name = "Maven for PR #2630" // https://github.com/neoforged/NeoForge/pull/2630
            url = uri("https://prmaven.neoforged.net/NeoForge/pr2630")
            content {
                includeModule("net.neoforged", "neoforge")
                includeModule("net.neoforged", "testframework")
            }
        }
        maven {
            name = "Maven for PR #357" // https://github.com/neoforged/FancyModLoader/pull/357
            url = uri("https://prmaven.neoforged.net/FancyModLoader/pr357")
            content {
                includeModule("net.neoforged.fancymodloader", "bootstraplauncher")
                includeModule("net.neoforged.fancymodloader", "earlydisplay")
                includeModule("net.neoforged.fancymodloader", "junit-fml")
                includeModule("net.neoforged.fancymodloader", "loader")
                includeModule("net.neoforged.fancymodloader", "securejarhandler")
            }
        }
        mavenCentral()
    }
}

rootProject.name = 'ae2';
