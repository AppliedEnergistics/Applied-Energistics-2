import appengbuild.ProjectDefaultsPlugin

plugins {
    id 'java-library'
    id "com.diffplug.spotless"
}

apply plugin: ProjectDefaultsPlugin

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'

    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'com.google.code.gson:gson:2.10'

    testCompileOnly 'org.jetbrains:annotations:23.0.0'
    testImplementation platform("org.junit:junit-bom:${project.junit_version}")
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.36"
}

test {
    useJUnitPlatform()
}

/////////////
// Spotless
spotless {
    java {
        target 'src/**/*.java'

        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile '../../codeformat/codeformat.xml'
        importOrderFile '../../codeformat/ae2.importorder'

        // courtesy of diffplug/spotless#240
        // https://github.com/diffplug/spotless/issues/240#issuecomment-385206606
        custom 'noWildcardImports', {
            if (it.contains('*;\n')) {
                throw new Error('No wildcard imports allowed')
            }
        }
        bumpThisNumberIfACustomStepChanges(1)
    }
}
