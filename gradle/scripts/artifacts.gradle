/*
 * This file is part of Applied Energistics 2.
 * Copyright (c) 2013 - 2014, AlgorithmX2, All rights reserved.
 *
 * Applied Energistics 2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Applied Energistics 2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Applied Energistics 2.  If not, see <http://www.gnu.org/licenses/lgpl>.
 */
 
// Suppresses warnings/errors when building javadocs
allprojects {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

javadoc {
	options.encoding = 'UTF-8'
	options.charSet = 'UTF-8'
}

task javadocs(type: Javadoc) {
	source = sourceSets.api.java
	include "appeng/api/**"

	// Due to ForgeGradle having to be a special snowflake,
	// we have to add some custom configurations to the normal compile configuration and hope it does not break.
	classpath = configurations.compile + configurations.forgeGradleMc + configurations.forgeGradleMcDeps 
}

task javadocJar(type: Jar, dependsOn: javadocs) {
	from javadoc.destinationDir

	classifier = 'javadoc'
}

task apiJar(type: Jar) {
	from sourceSets.api.java
	include "appeng/api/**"

	from sourceSets.api.output
	include "appeng/api/**"

	classifier = 'api'
}

task sourceJar(type: Jar, overwrite: true) {
	from sourceSets.api.allSource
	from sourceSets.main.allSource

	classifier = 'sources'
}

task devJar(type: Jar, overwrite: true) {
	from sourceSets.api.output
	from sourceSets.main.output

	classifier = 'dev'
}

artifacts {
	archives apiJar
	archives javadocJar
	archives sourceJar
	archives devJar
}